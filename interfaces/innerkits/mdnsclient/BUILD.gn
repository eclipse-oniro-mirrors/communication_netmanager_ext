# Copyright (C) 2023-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")
import("//foundation/communication/netmanager_ext/netmanager_ext_config.gni")

config("net_mdns_manager_if_config") {
  # header file path
  include_dirs = [
    "${target_gen_dir}",
    "$EXT_INNERKITS_ROOT/mdnsclient/include",
    "$EXT_INNERKITS_ROOT/mdnsclient/include/proxy",
  ]

  cflags = []
  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }

  cflags += [
    "-fstack-protector-strong",
    "-D_FORTIFY_SOURCE=2",
    "-O2",
  ]

  cflags_cc = [
    "-fstack-protector-strong",
    "-D_FORTIFY_SOURCE=2",
    "-O2",
  ]
}

ohos_source_set("mdns_parcel") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  branch_protector_ret = "pac_ret"

  sources = [
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/mdns_common.cpp",
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/mdns_service_info.cpp",
  ]

  include_dirs = [ "$EXT_INNERKITS_ROOT/mdnsclient/include" ]

  deps = [ "$NETMANAGER_EXT_ROOT/utils:net_manager_ext_common" ]

  external_deps = [
    "c_utils:utils",
    "netmanager_base:net_manager_common",
  ]

  external_deps += [ "hilog:libhilog" ]

  part_name = "netmanager_ext"
  subsystem_name = "communication"
}

idl_gen_interface("mdns_service_interface") {
  sources = [ "IMdnsService.idl" ]
  sources_callback = [
    "IDiscoveryCallback.idl",
    "IRegistrationCallback.idl",
    "IResolveCallback.idl",
  ]
}

config("mdns_service_interface_stub_config") {
  include_dirs = [
    "include",
    "${target_gen_dir}",
  ]
}

ohos_source_set("mdns_service_interface_stub") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":mdns_service_interface_stub_config" ]
  output_values = get_target_outputs(":mdns_service_interface")
  sources = []
  sources += filter_include(output_values, [ "*ice_stub.cpp" ])
  sources += filter_include(output_values, [ "*ack_proxy.cpp" ])
  deps = [ ":mdns_service_interface" ]
  external_deps = [
    "hilog:libhilog",
    "netmanager_base:net_manager_common",
    "samgr:samgr_proxy",
  ]
  part_name = "netmanager_ext"
  subsystem_name = "communication"
}

ohos_shared_library("mdns_manager_if") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  branch_protector_ret = "pac_ret"

  sources = [
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/mdns_client.cpp",
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/mdns_client_resume.cpp",
  ]

  output_values = get_target_outputs(":mdns_service_interface")
  sources += filter_include(output_values, [ "*ice_proxy.cpp" ])
  sources += filter_include(output_values, [ "*ack_stub.cpp" ])

  version_script = "libmdns_kits.map"

  public_configs = [ ":net_mdns_manager_if_config" ]

  deps = [
    ":mdns_parcel",
    "$NETMANAGER_EXT_ROOT/utils:net_manager_ext_common",
  ]

  deps += [ ":mdns_service_interface" ]

  external_deps = [
    "bounds_checking_function:libsec_shared",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]

  public_external_deps = [ "netmanager_base:net_manager_common" ]

  defines = [
    "NETMGR_LOG_TAG = \"MDnsManager\"",
    "LOG_DOMAIN = 0xD0015B0",
  ]

  if (enable_netmgr_ext_debug) {
    defines += [ "NETMGR_DEBUG" ]
  }

  external_deps += [ "hilog:libhilog" ]

  innerapi_tags = [ "platformsdk" ]
  part_name = "netmanager_ext"
  subsystem_name = "communication"
}
