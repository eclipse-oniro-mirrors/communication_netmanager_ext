# Copyright (c) 2022-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")
import("//foundation/communication/netmanager_ext/netmanager_ext_config.gni")

config("net_conn_manager_if_config") {
  include_dirs = [
    "${target_gen_dir}",
    "include",
    "include/proxy",
    "include/proxy/ipccallback",
    "$EXT_INNERKITS_ROOT/include",
  ]

  cflags = []
  if (is_double_framework) {
    cflags += [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }

  cflags += common_cflags

  cflags_cc = common_cflags
}

ohos_static_library("networkshare_parcel") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  branch_protector_ret = "pac_ret"

  sources = [ "$NETWORKSHAREMANAGER_INNERKITS_SOURCE_DIR/src/network_share_request_parcel.cpp" ]

  include_dirs = [
    "$EXT_INNERKITS_ROOT/include",
    "$EXT_INNERKITS_ROOT/netshareclient/include",
  ]

  external_deps = [
    "bounds_checking_function:libsec_shared",
    "netmanager_base:net_conn_manager_if",
    "netmanager_base:net_manager_common",
  ]

  external_deps += [ "hilog:libhilog" ]

  part_name = "netmanager_ext"
  subsystem_name = "communication"
}

idl_gen_interface("netshare_service_interface") {
  sources = [ "INetworkShareService.idl" ]
}
 
config("netshare_service_interface_stub_config") {
  include_dirs = [
    "${target_gen_dir}",
    "include",
    "include/proxy",
    "include/proxy/ipccallback",
    "$EXT_INNERKITS_ROOT/include",
  ]
}
 
ohos_static_library("netshare_service_interface_stub") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":netshare_service_interface_stub_config" ]
  output_values = get_target_outputs(":netshare_service_interface")
  sources = []
  sources += filter_include(output_values, [ "*ice_stub.cpp" ])
  sources += filter_include(output_values, [ "*ack_proxy.cpp" ])
  deps = [ ":netshare_service_interface" ]
  external_deps = [
    "hilog:libhilog",
    "netmanager_base:net_manager_common",
    "samgr:samgr_proxy",
    "c_utils:utils",
  ]
  part_name = "netmanager_ext"
  subsystem_name = "communication"
}

ohos_shared_library("net_tether_manager_if") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  branch_protector_ret = "pac_ret"

  include_dirs = [ "$EXT_INNERKITS_ROOT/include"]

  sources = [
    "$NETWORKSHAREMANAGER_INNERKITS_SOURCE_DIR/src/networkshare_client.cpp",
    "$NETWORKSHAREMANAGER_INNERKITS_SOURCE_DIR/src/proxy/ipccallback/netshare_result_callback_proxy.cpp",
    "$NETWORKSHAREMANAGER_INNERKITS_SOURCE_DIR/src/proxy/ipccallback/netshare_result_callback_stub.cpp",
    "$NETWORKSHAREMANAGER_INNERKITS_SOURCE_DIR/src/proxy/ipccallback/sharing_event_callback_proxy.cpp",
    "$NETWORKSHAREMANAGER_INNERKITS_SOURCE_DIR/src/proxy/ipccallback/sharing_event_callback_stub.cpp",
  ]

  output_values = get_target_outputs(":netshare_service_interface")
  sources += filter_include(output_values, [ "*ice_proxy.cpp" ])
  sources += filter_include(output_values, [ "*ack_stub.cpp" ])

  version_script = "libnetshare_kits.map"

  public_configs = [ ":net_conn_manager_if_config" ]

  deps = [ ":networkshare_parcel" ]
  deps += [ ":netshare_service_interface" ]

  external_deps = [
    "c_utils:utils",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]

  public_external_deps = [ "netmanager_base:net_manager_common" ]

  defines = [
    "NETMGR_LOG_TAG = \"NetworkShareManager\"",
    "LOG_DOMAIN = 0xD0015B0",
  ]

  if (enable_netmgr_ext_debug) {
    defines += [ "NETMGR_DEBUG" ]
  }

  external_deps += [ "hilog:libhilog" ]

  innerapi_tags = [ "platformsdk" ]
  part_name = "netmanager_ext"
  subsystem_name = "communication"
}
