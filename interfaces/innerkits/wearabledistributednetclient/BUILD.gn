# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")
import("//foundation/communication/netmanager_ext/netmanager_ext_config.gni")

config("net_conn_manager_if_config") {
  include_dirs = [
    "include",
    "$NETMANAGER_EXT_ROOT/utils/log/include",
    "$EXT_INNERKITS_ROOT/include",
    "$EXT_INNERKITS_ROOT/wearabledistributednetclient/include",
    "${target_gen_dir}",
  ]

  cflags = []
  if (is_double_framework) {
    cflags += [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }

  cflags += [
    "-fstack-protector-strong",
    "-D_FORTIFY_SOURCE=2",
    "-O2",
  ]

  cflags_cc = [
    "-fstack-protector-strong",
    "-D_FORTIFY_SOURCE=2",
    "-O2",
  ]
  defines = [
    "NETMGR_LOG_TAG = \"WearableDistributedNetManager\"",
    "LOG_DOMAIN = 0xD0015B0",
  ]

  if (enable_netmgr_ext_debug) {
    defines += [ "NETMGR_DEBUG" ]
  }
}

idl_interface_sources = [
  "${target_gen_dir}/wearable_distributed_net_proxy.cpp",
  "${target_gen_dir}/wearable_distributed_net_stub.cpp",
]

idl_gen_interface("wearable_distributed_net_service") {
  src_idl = rebase_path("IWearableDistributedNet.idl")
  dst_file = string_join(",", idl_interface_sources)
  hitrace = "HITRACE_TAG_ABILITY_MANAGER"
  log_domainid = "0xD0015B0"
  log_tag = "WearableDistributedNet"
}

ohos_shared_library("wearable_distributed_net_manager_if") {
  if (netmanager_ext_feature_wearable_distributed_net) {
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }

    branch_protector_ret = "pac_ret"

    sources = [
      "$WEARABLE_DISTRIBUTED_NET_INNERKITS_SOURCE_DIR/src/wearable_distributed_net_client.cpp",
      "${target_gen_dir}/wearable_distributed_net_proxy.cpp",
    ]

    version_script = "libwearable_distributed_net_kits.map"

    public_configs = [ ":net_conn_manager_if_config" ]
    deps = [ ":wearable_distributed_net_service" ]
    external_deps = [
      "bounds_checking_function:libsec_shared",
      "c_utils:utils",
      "hilog:libhilog",
      "hitrace:hitrace_meter",
      "ipc:ipc_core",
      "ipc:ipc_single",
      "netmanager_base:net_manager_common",
      "samgr:samgr_proxy",
    ]

    innerapi_tags = [ "platformsdk" ]
    part_name = "netmanager_ext"
    subsystem_name = "communication"
  }
}
